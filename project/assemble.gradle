apply plugin: 'java-library'

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = sourceCompatibility // default value

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
        vendor = JvmVendorSpec.ADOPTIUM
    }
}

ext.javaLintArgs = [
    '-Xlint:all',
    '-Xlint:-deprecation',
    '-Xlint:-path',
    '-Xlint:-serial'
]

ext.sharedManifest = manifest {
    attributes(
        'Implementation-Title':   project.name,
        'Implementation-Version': project.version,
        'Built-JDK':              System.getProperty('java.version'),
        'Built-By':               System.getProperty('user.name'),
        'Built-Date':             new Date()
    )

    if (pluginManager.hasPlugin('application') && application.getMainClass().isPresent()) {
        attributes 'Main-Class': application.getMainClass().get()
    }
}

task sourceJar(type: Jar) {
    group 'Build'
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    group 'Build'
    archiveClassifier = 'javadoc'
    from javadoc
}

jar {
    manifest = project.manifest { from sharedManifest }
}


tasks.withType(JavaCompile) {
    options.incremental = true
    options.deprecation = false
    options.compilerArgs += project.javaLintArgs
}
